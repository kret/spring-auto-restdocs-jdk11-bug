plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'org.asciidoctor.convert' version '1.6.0'
}

//sourceCompatibility = 1.8
sourceCompatibility = 11

repositories {
    jcenter()
}

configurations {
    all*.exclude group: 'junit', module: 'junit'
    jsondoclet
}

ext {
    springRestdocsVersion = '2.0.3.RELEASE'
    springAutoRestdocsVersion = '2.0.4'

    snippetsDir = file("$buildDir/generated-snippets")
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}
ext['junit-jupiter.version'] = '5.4.1'

dependencies {
    implementation(
      'org.springframework.boot:spring-boot-starter-web',
    )
    testImplementation(
      'org.junit.jupiter:junit-jupiter',
      "org.springframework.restdocs:spring-restdocs-core:${springRestdocsVersion}",
      "org.springframework.restdocs:spring-restdocs-mockmvc:${springRestdocsVersion}",
      'org.springframework.boot:spring-boot-starter-test',

      "capital.scalable:spring-auto-restdocs-core:${springAutoRestdocsVersion}",
    )
    jsondoclet(
//      "capital.scalable:spring-auto-restdocs-json-doclet:${springAutoRestdocsVersion}",
      "capital.scalable:spring-auto-restdocs-json-doclet-jdk9:${springAutoRestdocsVersion}",
    )
}

jar {
    dependsOn asciidoctor
}

bootJar {
    baseName = 'bug-reproduce-app'
    mainClassName = 'bug.reproduce.BugReproduceApp'
    dependsOn asciidoctor

    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
    options.memberLevel = JavadocMemberLevel.PACKAGE
}

test {
    useJUnitPlatform()

    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir

    dependsOn jsonDoclet
}

asciidoctor {
    sourceDir = file('src/docs/asciidoc')
    outputDir = file("$buildDir/generated-docs")
    options backend: 'html', doctype: 'book'
    attributes 'source-highlighter': 'highlightjs', 'snippets': snippetsDir

    dependsOn test
}
